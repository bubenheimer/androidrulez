/*
 * Copyright (c) 2015-2017 Uli Bubenheimer. All rights reserved.
 */

buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha13'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'net.ltgt.errorprone'

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

android {
    lintOptions {
        baseline file('lint-baseline.xml')
    }

    compileSdkVersion 28

    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 14
        versionCode 1
        versionName '1.0'
    }

    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // TODO change based on resolution of https://issuetracker.google.com/issues/72050365
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    errorprone 'com.google.errorprone:error_prone_core:2.3.2'

    // Give users access to BreadthFirstRuleEngine, etc.
    api 'org.bubenheimer:javarulez:1.0-SNAPSHOT'
    implementation 'org.bubenheimer:androidutil:1.0-SNAPSHOT'
    implementation 'androidx.annotation:annotation:1.0.0'
}
