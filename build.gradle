/*
 * Copyright (c) 2015-2017 Uli Bubenheimer. All rights reserved.
 */

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-beta01'
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

publishing {
    publications {
        library(MavenPublication) {
            artifact bundleReleaseAar

            pom.withXml {
                def deps = asNode().appendNode('dependencies')

                // Give users access to BreadthFirstRuleEngine, etc.
                def javarulez = deps.appendNode('dependency')
                javarulez.appendNode('groupId', 'org.bubenheimer')
                javarulez.appendNode('artifactId', 'javarulez')
                javarulez.appendNode('version', '1.0-SNAPSHOT')
            }
        }
    }
}

android {
    lintOptions {
        baseline file('lint-baseline.xml')
    }

    compileSdkVersion 28

    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 14
        versionCode 1
        versionName '1.0'
    }

    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    // TODO change based on resolution of https://issuetracker.google.com/issues/72050365
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }

    dependencies {
        implementation 'org.bubenheimer:javarulez:1.0-SNAPSHOT'
        implementation 'org.bubenheimer:androidutil:1.0-SNAPSHOT'
        implementation 'com.android.support:support-annotations:28.0.0-alpha1'
    }
}
